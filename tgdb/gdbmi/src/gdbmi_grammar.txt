output 					-> ( out-of-band-record )* [ result-record ] "(gdb)" nl
result-record 			-> [ token ] "^" result-class ( "," result )* nl
out-of-band-record 		-> async-record | stream-record
async-record 			-> exec-async-output | status-async-output | notify-async-output
exec-async-output 		-> [ token ] "*" async-output
status-async-output 	-> [ token ] "+" async-output
notify-async-output 	-> [ token ] "=" async-output
async-output 			-> async-class ( "," result )* nl
result-class 			-> "done" | "running" | "connected" | "error" | "exit"
async-class 			-> "stopped" | others (where others will be added depending on the needs--this is still in development).
result 					-> variable "=" value
variable 				-> string
value 					-> const | tuple | list
const 					-> c-string
tuple 					-> "{}" | "{" result ( "," result )* "}"
list 					-> "[]" | "[" value ( "," value )* "]" | "[" result ( "," result )* "]"
stream-record 			-> console-stream-output | target-stream-output | log-stream-output
console-stream-output 	-> "~" c-string
target-stream-output 	-> "@" c-string
log-stream-output 		-> "&" c-string
nl 						-> CR | CR-LF
token 					-> any sequence of digits.

transformations 

1. (out-of-band-record)* -> out_of_band_record_list
	out-of-band-record-list  -> out-of-band-record-list out-of-band-record | epsilon

2. [result-record]
	opt-result-record -> result-record | epsilon

3. [token]
	opt-token -> token | epsilon

4. ( "," result )*
	result-list-prime -> result-list | epsilon
	result-list       -> result-list "," result | "," result

5. ( "," value )*
	value-list-prime -> value-list | epsilon
	value-list       -> value-list "," value | "," value 

output 					-> oob-record-list opt-record "(gdb)" nl
oob-record-list 		-> oob-record-list oob-record | epsilon
opt-result-record       -> result-record | epsilon
result-record 			-> opt-token "^" result-class result-list-prime nl
oob-record 				-> async-record | stream-record
async-record 			-> exec-async-output | status-async-output | notify-async-output
exec-async-output 		-> opt-token "*" async-output
status-async-output 	-> opt-token "+" async-output
notify-async-output 	-> opt-token "=" async-output
async-output 			-> async-class result-list-prime nl
result-class 			-> "done" | "running" | "connected" | "error" | "exit"
async-class 			-> "stopped" 
result-list-prime 		-> result-list | epsilon
result-list       		-> result-list "," result | "," result
result 					-> variable "=" value
variable 				-> string
value-list-prime        -> value-list | epsilon
value-list              -> value-list "," value | "," value 
value 					-> const | tuple | list
const 					-> c-string
tuple 					-> "{}" | "{" result result-list-prime "}"
list 					-> "[]" | "[" value value-list-prime "]" | "[" result result-list-prime "]"
stream-record 			-> console-stream-output | target-stream-output | log-stream-output
console-stream-output 	-> "~" c-string
target-stream-output 	-> "@" c-string
log-stream-output 		-> "&" c-string
nl 						-> CR | CR-LF
opt-token               -> token | epsilon
token 					-> any sequence of digits.
